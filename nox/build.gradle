buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.0.0'
    classpath 'com.github.jcandksolutions.gradle:android-unit-test:2.1.1'
  }
}
apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

repositories {
  jcenter()
}

android {
  compileSdkVersion 21
  buildToolsVersion "21.1.2"

  defaultConfig {
    minSdkVersion 9
    targetSdkVersion 21
    versionCode 1
    versionName "1.0"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_6
    targetCompatibility JavaVersion.VERSION_1_6
  }
}

apply plugin: 'android-unit-test'

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:21.0.3'
  compile 'com.squareup.picasso:picasso:2.5.2'
  testCompile 'junit:junit:4.10'
  testCompile 'org.mockito:mockito-all:1.9.5'
  testCompile 'org.robolectric:robolectric:3.0-rc2'
}

task checkstyle(type: Checkstyle) {
  configFile file('../config/checkstyle/checkstyle.xml')
  source 'src'
  include '**/*.java'
  exclude '**/gen/**'

  classpath = files()
}

def coverageSourceDirs = ['src/main/java',
                          'src/gen']
task jacocoTestReport(type: JacocoReport, dependsOn: "testDebug") {
  group = "Reporting"
  description = "Generate Jacoco coverage reports after running tests."
  reports {
    html.enabled = true
  }

  // Class R is used, but usage will not be covered, so ignore this class from report
  classDirectories = fileTree(dir: './build/intermediates/classes/debug',
      excludes: ['**/R.class',
                 '**/R$*.class'])
  sourceDirectories = files(coverageSourceDirs)
  executionData = files('build/jacoco/testDebug.exec')
}

apply from: 'https://raw.githubusercontent.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'